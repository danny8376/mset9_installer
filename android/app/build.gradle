plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def passwordProperties = new Properties()
def passwordPropertiesFilePath = keystoreProperties.getProperty('passwordPropertiesFilePath')
if (passwordPropertiesFilePath) {
    def passwordPropertiesFile = rootProject.file(passwordPropertiesFilePath)
    if (passwordPropertiesFile.exists()) {
        passwordProperties.load(new FileInputStream(passwordPropertiesFile))
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def defaultAppName = "MSET9 Installer"

android {
    namespace "moe.saru.homebrew.console3ds.mset9_installer"
    //compileSdkVersion flutter.compileSdkVersion
    compileSdkVersion 34
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "moe.saru.homebrew.console3ds.mset9_installer_android"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        //minSdkVersion flutter.minSdkVersion
        //targetSdkVersion flutter.targetSdkVersion
        minSdkVersion 21
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'

        manifestPlaceholders.appName = "@string/appName"
    }

    signingConfigs {
        release {
            keyAlias = System.getenv("KEY_ALIAS") ?: keystoreProperties['keyAlias']
            keyPassword = System.getenv("KEY_PASS") ?: keystoreProperties['keyPassword'] ?: passwordProperties['keyPassword']
            def storeFilePath = System.getenv("STORE_FILE") ?: keystoreProperties['storeFile']
            storeFile = storeFilePath ? file(storeFilePath) : null
            storePassword = System.getenv("STORE_PASS") ?: keystoreProperties['storePassword'] ?: passwordProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-dbg"
            manifestPlaceholders.appName = "!D|$defaultAppName"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.documentfile:documentfile:1.0.1'
}
